Option Explicit
Option Base 1

Public Sub MatchArrays()

    Dim firstArray As Variant
    With ThisWorkbook.Worksheets(1)
        firstArray = .Range("A1").CurrentRegion
    End With

    Dim secondArray As Variant
    With ThisWorkbook.Worksheets(2)
        secondArray = .Range("A1").CurrentRegion
    End With

    Dim i As Long
    Dim dictKeyForLookup As String
    Dim dictValueToReturn As String

    Dim dict As Object
    Set dict = CreateObject("Scripting.Dictionary")
    dict.CompareMode = vbTextCompare

    For i = LBound(firstArray, 1) To UBound(firstArray, 1)
        dictKeyForLookup = firstArray(i, 4)
        dictValueToReturn = firstArray(i, 3)
        If Not dict.Exists(dictKeyForLookup) Then
            dict.Add Key:=dictKeyForLookup, Item:=dictValueToReturn
        End If
    Next i

    Dim arrayToPaste As Variant
    ReDim arrayToPaste(UBound(secondArray, 1), 1)
    For i = LBound(secondArray, 1) To UBound(secondArray, 1)
        If dict.Exists(secondArray(i, 1)) Then
            arrayToPaste(i, 1) = dict.Item(secondArray(i, 1))
        End If
    Next i

    With ThisWorkbook.Worksheets(1)
        .Range(.Cells(1, 4), .Cells(UBound(secondArray, 1), 4)).Value = arrayToPaste
    End With

End Sub
