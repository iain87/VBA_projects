Option Explicit

Public Sub MatchArrays()

    Dim workingArray As Variant
    Dim workingCell As Range
    Dim workingLastRow As Long
    Set workingCell = Application.InputBox("Select the first cell of your comparison column.", Type:=8)
    workingLastRow = workingCell.End(xlDown).Row
    With workingCell
        workingArray = .Parent.Range(.Address, Left(.Address, Len(.Address) - 1) & workingLastRow)
    End With

    Dim targetArray As Variant
    Dim targetCell As Range
    Set targetCell = Application.InputBox("Select the first cell of your target column.", Type:=8)
    With targetCell
        targetArray = .Parent.Range(.Address, Left(.Address, Len(.Address) - 1) & workingLastRow)
    End With

    Dim loopCount As Long
    
    ReDim Preserve workingArray(1 To UBound(workingArray, 1), 1 To UBound(workingArray, 2) + 1)
    For loopCount = LBound(workingArray, 1) To UBound(workingArray, 1)
        workingArray(loopCount, UBound(workingArray, 2)) = targetArray(loopCount, 1)
    Next loopCount
    
    Dim comparisonArray As Variant
    Dim comparisonCell As Range
    Dim comparisonLastRow As Long
    Set comparisonCell = Application.InputBox("Select the first cell of the column to match values with.", Type:=8)
    comparisonLastRow = comparisonCell.End(xlDown).Row
    With comparisonCell
        comparisonArray = .Parent.Range(.Address, Left(.Address, Len(.Address) - 1) & comparisonLastRow)
    End With

    Dim returnArray As Variant
    Dim returnCell As Range
    Set returnCell = Application.InputBox("Select the first cell from the column you wish to return values from.", Type:=8)
    With returnCell
        returnArray = .Parent.Range(.Address, Left(.Address, Len(.Address) - 1) & comparisonLastRow)
    End With

    ReDim Preserve comparisonArray(1 To UBound(comparisonArray, 1), 1 To UBound(comparisonArray, 2) + 1)
    For loopCount = LBound(comparisonArray, 1) To UBound(comparisonArray, 1)
        comparisonArray(loopCount, UBound(comparisonArray, 2)) = returnArray(loopCount, 1)
    Next loopCount
    
    With New Scripting.Dictionary
        .CompareMode = vbTextCompare

        For loopCount = LBound(comparisonArray, 1) To UBound(comparisonArray, 1)
            If Not .Exists(comparisonArray(loopCount, 1)) Then
                .Add comparisonArray(loopCount, 1), comparisonArray(loopCount, 2)
            End If
        Next loopCount

        Dim arrayToPaste As Variant
        ReDim arrayToPaste(1 To UBound(workingArray, 1), 1 To 1)
        For loopCount = LBound(workingArray, 1) To UBound(workingArray, 1)
            If Len(workingArray(loopCount, 2)) = 0 Then
                If .Exists(workingArray(loopCount, 1)) Then
                    arrayToPaste(loopCount, 1) = .Item(workingArray(loopCount, 1))
                End If
            Else
                arrayToPaste(loopCount, 1) = workingArray(loopCount, 2)
            End If
        Next loopCount
    End With

    With targetCell
        .Parent.Range(.Address, Left(.Address, Len(.Address) - 1) & workingLastRow) = arrayToPaste
    End With

End Sub
